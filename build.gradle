plugins {
	id 'org.springframework.boot' version '2.6.2'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'jacoco'
	id 'com.diffplug.spotless' version "6.3.0"
}

group = 'com.mitchelltford'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://m2.dv8tion.net/releases'
	}
}

dependencies {
	// This dependency is here as a way to mitigate CVE-2021-44228 until a new version of Spring is released
        implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
        
	implementation "com.diffplug.spotless:spotless-plugin-gradle:6.1.2"

	implementation 'com.discord4j:discord4j-core:3.2.1'

	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-logging'

	implementation 'com.sedmelluq:lavaplayer:1.3.78'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation "org.mockito:mockito-core:4.+"
	testImplementation 'org.assertj:assertj-core:3.22.0'

}

jacoco {
	toolVersion = "0.8.7"
	reportsDir = file("$buildDir/jacoco")
}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

// makes sure tests are ran before generating a test report
jacocoTestReport {
	dependsOn test
}

// Enforcing File Format
compileJava {
	dependsOn spotlessApply
}

spotless {
	java {
		// Use the default importOrder configuration
		importOrder()

		removeUnusedImports()

		googleJavaFormat()
	}
}

